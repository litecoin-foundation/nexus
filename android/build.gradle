buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 26
        compileSdkVersion = 35
        targetSdkVersion = 35
        ndkVersion = "27.1.12297006"
        kotlinVersion = "2.0.21"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")

        classpath("com.google.gms:google-services:4.3.14")
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { stream ->
        localProperties.load(stream)
    }
}

allprojects {
    repositories {
        maven {
          name = "GitHubPackages"
          url = uri("https://maven.pkg.github.com/flexa/flexa-android")
          credentials {
              username = localProperties.getProperty("gpr.user")
              password = localProperties.getProperty("gpr.key")
          }
      }
    }
}

// react-native-nitro-tor: build only 64-bit + x86, never armeabi-v7a
subprojects { p ->
    if (p.name == "react-native-nitro-tor") {
        p.ext.set("reactNativeArchitectures", "arm64-v8a,x86,x86_64")

        p.plugins.withId("com.android.library") {
            def androidExt = p.extensions.findByName("android")
            if (androidExt != null) {
                androidExt.defaultConfig {
                    ndk { abiFilters "arm64-v8a", "x86", "x86_64" }
                }
            }
        }
    }
}

apply plugin: "com.facebook.react.rootproject"
